ORG	0000H      
	LJMP	START
	ORG	000BH			T0中断
	LJMP	INT00

	ORG	0030H
START:	CLR	TR0
	MOV	TMOD,	#01H
	SETB	ET0
	SETB	EA
MUSIC0:	MOV	B,	#00H
	CLR	A
	MOV	40H,	A         ;40H存放取值的序号
MUSIC1:	MOV	DPTR,	#DAT
	MOV	A,	40H
	MOVC	A,	@A+DPTR
	JZ	END0       	   ;00H表示结束
	MOV	30H,	A
	ANL	A,	#0FH         ;低四位表示节拍
	MOV	31H,	A         ;31H存放节拍码
	MOV	A,	30H
	SWAP	A
	ANL	A,	#0FH
	MOV	32H,	A        ;32H存放音符频率代码
	CJNE	A,	#00H,	MUSIC2  
	CLR	TR0
	SJMP	DEL_MS
MUSIC2:	MOV	DPTR,	#TABF
	MOV	A,	32H         
	RL	A
	DEC	A
	MOV	34H,	A
	MOVC	A,	@A+DPTR
	MOV	TL0,	A          ;根据音符频率得到定时器的初值
	MOV	44H,	TL0        ;保存定时器初值低8位
	DEC	34H
	MOV	A,	34H
	MOVC	A,	@A+DPTR
	MOV	TH0,	A
	MOV	45H,	TH0	 ;保存定时器初值高8位
	SETB	TR0
DEL_MS:	MOV	33H,	31H       ;节拍码放入33H,作延时
	ACALL	DELAY
	MOV	A,	40H
	INC	A
	MOV	40H,	A
	SJMP	MUSIC1
END0:	MOV	33H,	#10H        ;歌曲结束，延时一秒后继续
	ACALL	DELAY
	SJMP	START

DELAY:	MOV	R2,	33H
DEL1:	MOV	R3,	#02               ；延时125ms
DEL2:	MOV	R4,	#100
DEL3:	MOV	R5,	#255
DEL4:	DJNZ	R5,	DEL4
	DJNZ	R4,	DEL3
	DJNZ	R3,	DEL2
	DJNZ	R2,	DEL1
	RET
      
INT00:	CLR	EA
	MOV	TH0,	45H
	MOV	TL0,	44H
	PUSH	ACC 
	CPL	P3.3
	POP	ACC
	SETB	EA
	RETI

DAT:   DB 64H,64H,74H,84H,84H,74H,64H,54H,44H,44H,54H,64H,66H,52H,58H      ;音乐代码，高四位表频率，低四位节拍
       DB 64H,64H,74H,84H,84H,74H,64H,54H,44H,44H,54H,64H,56H,42H,48H
       DB 54H,54H,64H,44H,54H,62H,72H,64H,54H,62H,72H,64H,54H,44H,54H,12H,64H
       DB 64H,64H,74H,84H,84H,74H,64H,54H,44H,44H,54H,64H,56H,42H,48H,00H
TABF:  DB 0FBH,04H,0FBH,0CCH,0FCH,0CH,0FCH,44H,0FCH,0ACH,0FDH,09H,0FDH,34H   ;低音5-中音4
       DB 0FDH,82H,0FDH,0C8H,0FEH,06H,0FEH,22H,0FEH,56H,0FEH,85H,0FEH,9AH,0FEH,0C1H ;中音5-高音5
       END 
